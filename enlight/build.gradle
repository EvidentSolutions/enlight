apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

description = 'Syntax highlighting and source code detection for various languages.'
group = 'fi.evident.enlight'
version = '0.2.3-SNAPSHOT'

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

repositories {
    mavenCentral()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar

    archives javadocJar
    archives sourceJar
}

signing {
    sign configurations.archives
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(uploadArchives)) {
        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }

                    pom.project {
                        name 'enlight'
                        packaging 'jar'
                        description 'Syntax highlighting and source code detection for various languages.'
                        url 'https://bitbucket.org/evidentsolutions/enlight'
                        inceptionYear '2011'

                        issueManagement {
                            system 'Bitbucket'
                            url 'https://bitbucket.org/evidentsolutions/enlight/issues'
                        }

                        licenses {
                            license {
                                name 'MIT License'
                                url 'http://opensource.org/licenses/mit-license.php'
                                distribution 'repo'
                            }
                        }

                        organization {
                            name 'Evident Solutions'
                            url 'http://www.evident.fi'
                        }

                        developers {
                            developer {
                                id 'komu'
                                name 'Juha Komulainen'
                            }
                        }

                        scm {
                            url 'https://bitbucket.org/evidentsolutions/enlight'
                            connection 'scm:git:https://bitbucket.org/evidentsolutions/enlight.git'
                            developerConnection 'scm:git:ssh://git@bitbucket.org/evidentsolutions/enlight.git'
                        }
                    }
                }
            }
        }
    }
}
